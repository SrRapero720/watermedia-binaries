import groovy.json.JsonOutput

base {
    archivesName = project.id
    version = project.version
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabricmc.net/' }
    maven { url 'https://repo.spongepowered.org/maven' }
}

dependencies {
    compileOnly 'org.apache.logging.log4j:log4j-api:2.15.0'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.17.1'

    // TRULLY DEPENDENCY
    compileOnly project(':')

    // MOCKERS
    compileOnly project(':lib-neoforge')
    compileOnly project(':lib-forge')
    compileOnly("net.fabricmc:fabric-loader:${fbversion}") { // FABRIC LOADER SUPPORT
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
}
// Process target resources with mod info
processResources {
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta', 'fabric.mod.json', "mcmod.info", "watermedia/version.cfg"]) {
        def combinedMap = [:]
        combinedMap.putAll(rootProject.properties)
        combinedMap.putAll([
                modauthorlist: JsonOutput.toJson(modauthor.trim().replaceAll(" ", "").split(","))
        ])

        expand(combinedMap)
    }
    outputs.upToDateWhen { false }
}

jar {
    manifest {
        attributes([
                "Specification-Title"       : name,
                "Specification-Vendor"      : modauthor,
                "Specification-Version"     : version,
                "Implementation-Title"      : name,
                "Implementation-Version"    : version,
                "Implementation-Vendor"     : modauthor,
                "Implementation-Timestamp"  : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "ForceLoadAsMod"            : "true"
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    dependsOn(project.rootProject.tasks.shadowJar)
}